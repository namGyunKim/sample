plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.5'
    id 'io.spring.dependency-management' version '1.1.5'
}

group = 'com.sample'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

bootJar {
    archiveFileName.set('app.jar')
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

jar {
    enabled = false
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2023.0.1"
        mavenBom "software.amazon.awssdk:bom:2.20.26"
    }
}

dependencies {
    // =================================================================================
    // Web & Core
    // =================================================================================
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-aop'

    // Thymeleaf (View Template Engine)
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect' // 레이아웃 기능 지원

    // =================================================================================
    // JPA & Database
    // =================================================================================
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // Database Drivers (Runtime Only)
    runtimeOnly 'org.postgresql:postgresql'

    // Redis (Session & Cache)
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    implementation 'org.springframework.session:spring-session-data-redis'

    // =================================================================================
    // Security & Auth
    // =================================================================================
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.thymeleaf.extras:thymeleaf-extras-springsecurity6' // 타임리프 시큐리티 확장
    testImplementation 'org.springframework.security:spring-security-test'

    // Crypto (Bouncy Castle - 암호화 유틸)
    implementation 'org.bouncycastle:bcprov-jdk18on:1.77'

    // =================================================================================
    // Tools & Utils
    // =================================================================================
    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Configuration Processor (yml 자동완성 지원)
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    // DevTools (Live Reload 등)
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Swagger (API Documentation)
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // Logging (P6Spy - 쿼리 로그 파라미터 바인딩 확인)
    implementation 'com.github.gavlyukovskiy:p6spy-spring-boot-starter:1.9.0'

    // Rate Limiting (Bucket4j)
    implementation 'com.github.vladimir-bukhtoyarov:bucket4j-core:7.0.0'

    // Mail
    implementation 'org.springframework.boot:spring-boot-starter-mail'

    // =================================================================================
    // External Services & File Processing
    // =================================================================================
    // AWS S3
    implementation 'software.amazon.awssdk:s3'

    // Image Processing (TwelveMonkeys)
    implementation 'com.twelvemonkeys.imageio:imageio-core:3.10.1'
    implementation 'com.twelvemonkeys.imageio:imageio-webp:3.10.1'

    // =================================================================================
    // JSON / XML / Feign Client
    // =================================================================================
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "io.github.openfeign:feign-jackson"

    // JAXB (XML Processing - Java 9+ 호환성)
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    runtimeOnly 'com.sun.xml.bind:jaxb-impl:4.0.0'
    runtimeOnly 'org.glassfish.jaxb:jaxb-runtime'

}

tasks.named('test') {
    useJUnitPlatform()
}